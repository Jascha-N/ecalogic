import component controller
import component hdd

function get_data(T)
  while controller::peek() = 0 bound T do 
    skip 
  end while
  get_data := controller::read()
end function

function alwaysOn(N, T)
  hdd::on()
  while controller::engaged() > 0 bound N do
    X := get_data(T)
    hdd::write(X)
  end while
  hdd::off()
end function

function onDemand(N, T)
  while controller::engaged() > 0 bound N do
    X := get_data(T)
    hdd::on()
    hdd::write(X)
    hdd::off()
  end while
end function


(* the following demonstrates that our analysis is heavily dependent loop factorization *)

function caching(N, T, B)
  while controller::engaged() > 0 bound N do
    X := get_data(T)
    Q := controller::queue(X)
    if Q >= B then
      hdd::on()
      while Q > 0 bound B do
        X := controller::pop()
        hdd::write(X)
        Q := Q-1
      end while
      hdd::off()
    else
      skip
    end if
  end while
end function

function caching2(NB, T, B)
  while controller::engaged() > 0 bound NB do
    Q := 0
    while controller::engaged() > 0 bound B do
      X := get_data(T)
      Q := controller::queue(X)
    end while

    hdd::on()
    while Q > 0 bound B do
      X := controller::pop()
      hdd::write(X)
      Q := Q-1
    end while
    hdd::off()
  end while
end function
  

(*

Nice results:

$ scala nl.ru.cs.ecalogic.ECALogic -e onDemand hdd.eca
hdd.eca:
Time:   1*N*T + 106*N
Energy: 115*N
└           CPU 0
└    controller 0
└           hdd 115*N
$ scala nl.ru.cs.ecalogic.ECALogic -e alwaysOn hdd.eca
hdd.eca:
Time:   N*T + 6*N + 100
Energy: N*T + 11*N + 105
└           CPU 0
└    controller 0
└           hdd N*T + 11*N + 105

Less nice (since it appears to be outperformed):

$ scala nl.ru.cs.ecalogic.ECALogic -e caching hdd.eca
hdd.eca:
Time:   5*B*N + N*T + 102*N
Energy: 10*B*N + 106*N
└           CPU 0
└    controller N
└           hdd 10*B*N + 105*N

$ scala nl.ru.cs.ecalogic.ECALogic -e caching2 hdd.eca
hdd.eca:
Time:   B*NB*T + 7*B*NB + 100*NB
Energy: 11*B*NB + 105*NB
└           CPU 0
└    controller B*NB
└           hdd 10*B*NB + 105*NB

And since B*NB = N, we get:

Time:   N*T + 7*N + 100*NB
Energy: 11*N + 105*NB

*)

